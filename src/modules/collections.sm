@ Stack {

    create size:-/ * {
        'stack : [];
        'size : size;
    }

    push data * {
        ? 'isFull() { "The stack is full (size " + 'size.toString() + ")"!!!; }
        'stack +: [data];
    }

    pushAll data * { ... element ->? data { 'push(element); } }

    special * { * 'stack$; }

    pop * {
        ? 'isEmpty() { "The stack is empty"!!!; }
        out : 'stack<<-/>>;
        'stack -: 'stack$ - /;
        * out;
    }

    peek * {
        ? 'isEmpty() { "The stack is empty"!!!; }
        * 'stack<<-/>>;
    }

    toBoolean * { * 'stack$ > \; }

    isFull * { * 'size :: 'stack$; }

    isEmpty * { * 'stack$ :: \; }

    toString * {
        ?? { top : 'peek(); }
        !! { top : _; }
        * "Stack(capacity=" + 'size.toString()
        + ", size=" + 'stack$.toString()
        + ", top=" + top.toString()
        + ")";
    }
}